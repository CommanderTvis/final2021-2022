//@strict-types

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если СтрокиКлиентСервер.СтрЧислоЦифр(Номер) = 10 И КодСтраны <> Справочники.КодыСтран.ПустаяСсылка() Тогда
		НомерТелефона = "+" + КодСтраны.КодСтраны + Номер;
	КонецЕсли;

	Если Не ПроверкаУникальностиВызовСервера.ПрошелКонтрольУникальностиСправочникаПоРеквизиту(
		Новый Структура("Наименование", Наименование), Ссылка) Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Логин должен быть уникальным.", "Наименование", ЭтотОбъект);
		Отказ = Истина;
	КонецЕсли;

	Если Не ПроверкаУникальностиВызовСервера.ПрошелКонтрольУникальностиСправочникаПоРеквизиту(
		Новый Структура("НомерТелефона", НомерТелефона), Ссылка) Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Номер телефона должен быть уникальным.", "НомерТелефона", ЭтотОбъект);
		Отказ = Истина;
	КонецЕсли;

	Если Не ПроверкаУникальностиВызовСервера.ПрошелКонтрольУникальностиСправочникаПоРеквизиту(
		Новый Структура("ЭлектроннаяПочта", ЭлектроннаяПочта), Ссылка) Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Электронная почта должна быть уникальной.", "ЭлектроннаяПочта", ЭтотОбъект);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Не (СтрокиКлиентСервер.СтрЧислоЦифр(Номер) = 0 И КодСтраны = Справочники.КодыСтран.ПустаяСсылка()) И КодСтраны
		= Справочники.КодыСтран.ПустаяСсылка() Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Нужно выбрать код страны.", "КодСтраны", ЭтотОбъект);
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Не (СтрокиКлиентСервер.СтрЧислоЦифр(Номер) = 0 И КодСтраны = Справочники.КодыСтран.ПустаяСсылка())
		И СтрокиКлиентСервер.СтрЧислоЦифр(Номер) <> 10 Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Нужно ввести номер телефона.", "Номер", ЭтотОбъект);
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ЭлектроннаяПочта) И Не РегулярныеВыраженияВызовСервера.ПроверитьСтроку(ЭлектроннаяПочта,
		"\S+@\S+\.\S+") Тогда
		СообщенияКлиентСервер.СообщитьСоСвойствами("Неправильный формат адреса.", "ЭлектроннаяПочта", ЭтотОбъект);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры