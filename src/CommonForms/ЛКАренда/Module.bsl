#Если Не ВебКлиент Тогда
&НаКлиенте
Асинх Процедура Завершить(Команда)
	Для Каждого Идентификатор Из Элементы.СейчасАрендованы.ВыделенныеСтроки Цикл
		Товар = Элементы.СейчасАрендованы.ДанныеСтроки(Идентификатор).ТоварСсылка;
		Станция = Ждать ВвестиЗначениеАсинх(,"На какой станции закончить аренду " + Товар,Тип("СправочникСсылка.Станции"));
		ЗавершитьОдин(Товар, Станция);
	КонецЦикла;
	Элементы.СейчасАрендованы.Обновить();
КонецПроцедуры

&НаСервере
Процедура ЗавершитьОдин(Товар, Станция)
	ТекДата = ТекущаяДатаСеанса();
	ДатаНачала = РегистрыСведений.СостояниеНоменклатуры.СрезПоследних(ТекДата, Новый Структура("Товар",
		Товар))[0].Период;

	НачатьТранзакцию();
	Попытка
		Документ = Документы.ОкончаниеАренды.СоздатьДокумент();
		Документ.Клиент = Клиент;
		Документ.Товар = Товар;
		Документ.Дата = ТекДата;
		Документ.Станция = Станция;
		Документ.Записать(РежимЗаписиДокумента.Проведение);

		Документ = Документы.ОплатаАренды.СоздатьДокумент();
		Документ.Клиент = Клиент;
		Документ.Товар = Товар;
		Документ.Дата = ТекДата;

		Документ.ВремяАренды = Цел(ТекДата - ДатаНачала) / 60;
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		ОплатаАренды = Документ.Ссылка;
		Бонус = Цел(ОплатаАренды.СуммаОплаты / 10);
		
		Если Бонус > 0 Тогда
		Документ = Документы.ПолучениеБонусныхБаллов.СоздатьДокумент();
		Документ.Причина = Перечисления.ПричиныПолученияБаллов.ЗаАренду;
		Документ.КоличествоБаллов = Бонус;
		Документ.Клиент = Клиент;
		Документ.Записать();
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьИзСписка1(Команда)
	Товар = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	Товар = Ждать ВвестиЗначениеАсинх(Товар, "Введите товар из списка.");

	Если ЗначениеЗаполнено(Товар) Тогда
		ВзятьВАренду();
	КонецЕсли;
КонецПроцедуры

// @strict-types
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Клиент = ПользователиВызовСервера.ТекущийКлиент();
	СейчасАрендованы.Параметры.УстановитьЗначениеПараметра("Арендатор", Клиент);
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВвестиАртикулИлиНаименование(Команда)
	Текст = Ждать ВвестиСтрокуАсинх(,"Введите артикул или часть наименования.");
	ОбработкаВвода(Текст);

	Если ЗначениеЗаполнено(Товар) Тогда
		Рез = Ждать ВопросАсинх("Подтвердить аренду товара " + Товар.Наименование + "?", РежимДиалогаВопрос.ОКОтмена);
		Если Рез = КодВозвратаДиалога.ОК Тогда
			ВзятьВАренду();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВзятьВАренду()
	Документ = Документы.НачалоАренды.СоздатьДокумент();
	Документ.Дата = ТекущаяДатаСеанса();
	Документ.Товар = Товар;
	Документ.Клиент = Клиент;
	Документ.Записать(РежимЗаписиДокумента.Проведение);
	Элементы.СейчасАрендованы.Обновить();
КонецПроцедуры

// Обрабатывает введенный пользователем текст, содержащий либо артикул, либо наименование товара.
// 
// Параметры:
//   Текст - Строка, Неопределено - Введенный текст.
&НаСервере
Процедура ОбработкаВвода(Текст)
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Товар = Справочники.Номенклатура.НайтиПоКоду(Текст);

	Если Не ЗначениеЗаполнено(Товар) Тогда
		Товар = Справочники.Номенклатура.НайтиПоНаименованию(Текст);
	КонецЕсли;
КонецПроцедуры
#КонецЕсли